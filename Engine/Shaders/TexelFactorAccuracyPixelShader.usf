// Copyright 1998-2016 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	TexelFactorAccuracyPixelShader.usf: Outputs the accucary of the texel factor.
=============================================================================*/

#include "Common.usf"

#include "DebugViewModeCommon.usf"

float4 AccuracyColors[5];
float CPUTexelFactor; // In world space units.

// Needed because UAV accesses disable early depth test otherwises.
EARLYDEPTHSTENCIL
void Main(
	in FDebugPSIn DebugInputs,
	out float4 OutColor : SV_Target0
	)
{
#if INSTANCED_STEREO
	ResolvedView = ResolveView(DebugInputs.EyeIndex);
#else
	ResolvedView = ResolveView();
#endif

	float3 Result = float3(.1, .1, .1);

	if (CPUTexelFactor >= 0)
	{
		float3 WorldPosition = SvPositionToResolvedTranslatedWorld(DebugInputs.SvPosition);

		float2 CoordDDX = ddx(DebugInputs.TexCoord0.xy);
		float2 CoordDDY = ddy(DebugInputs.TexCoord0.xy);
		float3 WorldPosDDX = ddx(WorldPosition);
		float3 WorldPosDDY = ddy(WorldPosition);

		float GPUTexelFactor = max(length(WorldPosDDX) / length(CoordDDX), length(WorldPosDDY) / length(CoordDDY));

		float Accuracy = clamp(log2(CPUTexelFactor) - log2(GPUTexelFactor), -1.99, 1.99);
		int ColorIndex = floor(Accuracy) + 2;

		Result = lerp(AccuracyColors[ColorIndex], AccuracyColors[ColorIndex + 1], frac(Accuracy));
	}

	OutColor = RETURN_COLOR(float4(Result, .25f));
}
